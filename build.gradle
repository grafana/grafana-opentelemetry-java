group 'com.grafana'
version '2.4.0-beta.1'

buildscript {
  ext {
    otelInstrumentationVersion = "2.4.0"
  }
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.25.0"
    classpath "gradle.plugin.com.github.johnrengelman:shadow:8.0.0"
    classpath "io.opentelemetry.instrumentation:gradle-plugins:${otelInstrumentationVersion}-alpha"
  }
}

subprojects {
  version = rootProject.version

  apply plugin: "java"
  apply plugin: "com.diffplug.spotless"

  ext {
    versions = [
      // this line is managed by .github/scripts/update-sdk-version.sh
      opentelemetrySdk           : "1.38.0",

      // these lines are managed by .github/scripts/update-version.sh
      opentelemetryJavaagent     : otelInstrumentationVersion,
      opentelemetryJavaagentAlpha: "${otelInstrumentationVersion}-alpha",

      bytebuddy                  : "1.14.16",
      autoservice                : "1.1.1",
      junit                      : "5.10.2",
      logUnit                    : "2.0.0",
      assertj                    : "3.25.3",
    ]

    deps = [
      bytebuddy  : "net.bytebuddy:byte-buddy-dep:${versions.bytebuddy}",
      autoservice: [
        "com.google.auto.service:auto-service:${versions.autoservice}",
        "com.google.auto.service:auto-service-annotations:${versions.autoservice}",
      ]
    ]
  }

  repositories {
    mavenCentral()
  }

  spotless {
    java {
      googleJavaFormat()
      licenseHeaderFile(rootProject.file("buildscripts/spotless.license.java"), "(package|import|public)")
      target("src/**/*.java")
    }

    format 'misc', {
      // not using "**/..." to help keep spotless fast
      target(
        ".gitignore",
        ".gitattributes",
        ".gitconfig",
        ".editorconfig",
        "*.md",
        "src/**/*.md",
        "docs/**/*.md",
        "*.sh",
        "src/**/*.properties",
      )
      indentWithSpaces()
      trimTrailingWhitespace()
      endWithNewline()
    }
  }

  dependencies {
    implementation(platform("io.opentelemetry:opentelemetry-bom:${versions.opentelemetrySdk}"))

    // these serve as a test of the instrumentation boms
    implementation(platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:${versions.opentelemetryJavaagent}"))
    implementation(platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:${versions.opentelemetryJavaagentAlpha}"))

    testImplementation("org.assertj:assertj-core:${versions.assertj}")

    testImplementation(enforcedPlatform("org.junit:junit-bom:${versions.junit}"))
    testImplementation("org.junit.jupiter:junit-jupiter-api:${versions.junit}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${versions.junit}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${versions.junit}")

    testImplementation("io.github.netmikey.logunit:logunit-core:${versions.logUnit}")
    testRuntimeOnly("io.github.netmikey.logunit:logunit-jul:${versions.logUnit}")
  }

  tasks {
    test {
      useJUnitPlatform()
    }

    compileJava {
      options.release.set(8)
      options.compilerArgs.addAll(["-Werror", "-Xlint:-options"]) //Java 8 is deprecated as of JDK 21
    }
    compileTestJava {
      options.release.set(17)
    }
  }
}
